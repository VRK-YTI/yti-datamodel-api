buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.24.16')
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "org.springframework.boot" version "2.5.12"
    id "org.sonarqube" version "3.3"
    id "org.owasp.dependencycheck" version "6.1.6"
    id "com.github.ManifestClasspath" version "0.1.0-RELEASE"
    id "com.gorylenko.gradle-git-properties" version "2.3.1"
    id "com.github.ben-manes.versions" version "0.39.0"
}

apply plugin: "java"
apply plugin: 'io.spring.dependency-management'
apply plugin: "idea"
apply plugin: "com.jfrog.artifactory"
apply plugin: "com.github.ManifestClasspath"

allprojects {
    if(project.convention.findPlugin(JavaPluginConvention)) {
        // Change the output directory for the main and test source sets back to the old path
        sourceSets.main.java.destinationDirectory.value(project.getLayout().getBuildDirectory().dir("classes/${sourceSets.main.name}"));
        sourceSets.test.java.destinationDirectory.value(project.getLayout().getBuildDirectory().dir("classes/${sourceSets.test.name}"));

    }
}

artifactoryPublish.skip = true

archivesBaseName = 'yti-datamodel-api'

repositories {
    mavenLocal()
    mavenCentral {
        content { excludeGroupByRegex "fi\\.vm\\.yti.*" }
    }
    google {
        content { excludeGroupByRegex "fi\\.vm\\.yti.*" }
    }
    jcenter {
        content { excludeGroupByRegex "fi\\.vm\\.yti.*" }
    }

    // put your artifactory parameters to $HOME/.gradle/gradle.properties
    if (project.hasProperty('artifactoryUrl')) {
        maven {
            url "$artifactoryUrl"
            credentials {
                username "$artifactoryUsername"
                password "$artifactoryPassword"
            }
        }
    }
}

sourceCompatibility = 11
targetCompatibility = 11

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2020.0.5'
    }
}

configurations {
    all {
        resolutionStrategy.cacheDynamicVersionsFor 0, "seconds"
        resolutionStrategy.cacheChangingModulesFor 0, "seconds"
    }
    swaggerUI { transitive = false }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-jersey"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-jetty"

    implementation "org.springframework.security:spring-security-core"
    implementation "org.springframework.security:spring-security-config"
    implementation "org.springframework.security:spring-security-web"

    implementation "fi.vm.yti:yti-spring-security:9.9.8"
    implementation "fi.vm.yti:yti-spring-migration:0.2.0"

    compileOnly "org.springframework.boot:spring-boot-configuration-processor"

    implementation "org.springframework.cloud:spring-cloud-starter-config"
    implementation "org.springframework.cloud:spring-cloud-starter-sleuth"

    implementation "ch.qos.logback:logback-core"
    implementation 'net.logstash.logback:logstash-logback-encoder:7.2'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation 'org.elasticsearch:elasticsearch:6.8.22'
    implementation 'org.elasticsearch.client:elasticsearch-rest-client:6.8.22'
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:6.8.22'
    implementation "com.atlassian.commonmark:commonmark:0.17.0"
    implementation group: 'org.json', name: 'json', version: '20220320'
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation 'com.jayway.jsonpath:json-path:2.7.0'
    implementation group: 'io.swagger.core.v3', name: 'swagger-core', version: '2.1.10'
    implementation group: 'io.swagger.core.v3', name: 'swagger-jaxrs2', version: '2.1.10'
    swaggerUI 'org.webjars:swagger-ui:3.52.1'

    implementation 'org.topbraid:shacl:1.4.2'
    implementation "org.apache.jena:jena-core:4.6.1"
    implementation "org.apache.jena:jena-arq:4.6.1"
    implementation "org.apache.jena:jena-rdfconnection:4.6.1"
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
    implementation "org.glassfish.jersey.media:jersey-media-sse:2.34"
    implementation "com.github.jsonld-java:jsonld-java:0.12.0"
    implementation 'org.jboss.forge.roaster:roaster-api:2.26.0.Final'
    implementation 'org.jboss.forge.roaster:roaster-jdt:2.26.0.Final'
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'com.google.guava:guava:31.1-jre'

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'com.jayway.jsonpath:json-path-assert:2.7.0'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.12.16'
}

compileJava.dependsOn(processResources)

bootRun {
    systemProperties System.properties
    jvmArgs = ["-Dspring.output.ansi.enabled=ALWAYS"]
}

springBoot {
    mainClassName = "fi.vm.yti.datamodel.api.Application"
}

sonarqube {
    properties {
        property "sonar.projectKey", "yti-datamodel-api"
        property "sonar.projectName", "yti-datamodel-api"
        property("sonar.dependencyCheck.reportPath", "$buildDir/reports/dependency-check-report.xml")
        property("sonar.dependencyCheck.htmlReportPath", "$buildDir/reports/dependency-check-report.html")
    }
}

dependencyCheck {
    format = 'ALL'
    autoUpdate = false
    analyzers {
        centralEnabled = false
    }
    data {
        directory = System.getenv('OWASP_DATA_DIR')
    }
}

task unzipSwagger(type:Copy) {
    from zipTree(configurations.swaggerUI.singleFile)
    include "META-INF/resources/webjars/swagger-ui/3.52.1/**"
    into "build/classes/main/static/swagger-ui"
    eachFile { f ->
        if (f.path.endsWith('index.html')) {
            filter { line ->
                line.replace('"https://petstore.swagger.io/v2/swagger.json"', '"/datamodel-api/api/openapi.yaml", docExpansion: "none", validatorUrl: null')
            }
        }
        f.path = f.path.replaceFirst('META-INF/resources/webjars/swagger-ui/3.52.1/', '')
    }
    includeEmptyDirs = false
}

build.dependsOn unzipSwagger
bootJar.dependsOn unzipSwagger
